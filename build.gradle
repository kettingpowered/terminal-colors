plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

group = 'org.kettingpowered'
version = '1.0.0'

repositories {
    mavenCentral()
}

configurations {
    configureEach {
        resolutionStrategy {
            force 'org.fusesource.jansi:jansi:1.18'
        }
    }
}

dependencies {
    implementation libs.bundles.jline.packed
    implementation libs.terminalconsoleappender
    implementation libs.jansi
    implementation libs.jline
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]

    archiveBaseName.set('terminal-colors')
    archiveClassifier.set('')
    archiveExtension.set('jar')

    manifest {
        attributes 'Specification-Title': 'Kettingpowered',
                'Specification-Vendor': 'Kettingpowered',
                'Implementation-Title': 'terminal-colors',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Kettingpowered'
    }

    mergeServiceFiles()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            artifact(sourcesJar) {
                // These values will be used instead of the values from the task. The task values will not be updated.
                classifier "sources"
                extension "jar"
            }
            artifact(javadocJar) {
                //These values will be used instead of the values from the task. The task values will not be updated.
                classifier "javadoc"
                extension "jar"
            }
//        artifact jar
//        groupId = "org.kettingpowered"
//        artifactId = "terminal-colors"
            version = System.getenv("VERSION")
            pom {
                name = "KettingCore"
                description = "An API supported across all Ketting Versions"
                url = "https://github.com/kettingpowered/terminal-colors"
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                developers {
                    developer {
                        id = "justred23"
                        name = "JustRed23"
                        //email = "jon@doe"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kettingpowered/terminal-colors.git"
                    //developerConnection = "scm:svn:https://subversion.example.com/svn/project/trunk/"
                    url = "https://github.com/kettingpowered/terminal-colors"
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kettingpowered/terminal-colors")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = 'kettingRepo'
            credentials {
                username = System.getenv("KETTINGUSERNAME")
                password = System.getenv("KETTINGPASSWORD")
            }
            url = "https://nexus.c0d3m4513r.com/repository/Ketting/"
        }
    }
}